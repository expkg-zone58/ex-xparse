<?xqlint
var XQueryParser = exports.XQueryParser = function XQueryParser(string, parsingEventHandler)
{
  init(string, parsingEventHandler);
?>

/* XQuery 3.1 with fulltext and update version for BaseX 
@ date 2022-09-20
*/
XQuery   ::= Module EOF
Module   ::= VersionDecl? ( LibraryModule | MainModule )
VersionDecl
         ::= 'xquery' ( 'encoding' StringLiteral | 'version' StringLiteral ( 'encoding' StringLiteral )? ) Separator
MainModule
         ::= Prolog QueryBody
LibraryModule
         ::= ModuleDecl Prolog
ModuleDecl
         ::= 'module' 'namespace' NCName '=' URILiteral Separator
Prolog   ::= ( ( DefaultNamespaceDecl | Setter | NamespaceDecl | Import | FTOptionDecl ) Separator )* ( ( ContextItemDecl | AnnotatedDecl | OptionDecl ) Separator )*
Separator
         ::= ';'
Setter   ::= BoundarySpaceDecl
           | DefaultCollationDecl
           | BaseURIDecl
           | ConstructionDecl
           | OrderingModeDecl
           | EmptyOrderDecl
           | RevalidationDecl
           | CopyNamespacesDecl
           | DecimalFormatDecl
BoundarySpaceDecl
         ::= 'declare' 'boundary-space' ( 'preserve' | 'strip' )
DefaultCollationDecl
         ::= 'declare' 'default' 'collation' URILiteral
BaseURIDecl
         ::= 'declare' 'base-uri' URILiteral
ConstructionDecl
         ::= 'declare' 'construction' ( 'strip' | 'preserve' )
OrderingModeDecl
         ::= 'declare' 'ordering' ( 'ordered' | 'unordered' )
EmptyOrderDecl
         ::= 'declare' 'default' 'order' 'empty' ( 'greatest' | 'least' )
CopyNamespacesDecl
         ::= 'declare' 'copy-namespaces' PreserveMode ',' InheritMode
PreserveMode
         ::= 'preserve'
           | 'no-preserve'
InheritMode
         ::= 'inherit'
           | 'no-inherit'
DecimalFormatDecl
         ::= 'declare' ( 'decimal-format' EQName | 'default' 'decimal-format' ) ( DFPropertyName '=' StringLiteral )*
DFPropertyName
         ::= 'decimal-separator'
           | 'grouping-separator'
           | 'infinity'
           | 'minus-sign'
           | 'NaN'
           | 'percent'
           | 'per-mille'
           | 'zero-digit'
           | 'digit'
           | 'pattern-separator'
           | 'exponent-separator'
Import   ::= SchemaImport
           | ModuleImport
SchemaImport
         ::= 'import' 'schema' SchemaPrefix? URILiteral ( 'at' URILiteral ( ',' URILiteral )* )?
SchemaPrefix
         ::= 'namespace' NCName '='
           | 'default' 'element' 'namespace'
ModuleImport
         ::= 'import' 'module' ( 'namespace' NCName '=' )? URILiteral ( 'at' URILiteral ( ',' URILiteral )* )?
NamespaceDecl
         ::= 'declare' 'namespace' NCName '=' URILiteral
DefaultNamespaceDecl
         ::= 'declare' 'default' ( 'element' | 'function' ) 'namespace' URILiteral
FTOptionDecl
         ::= 'declare' 'ft-option' FTMatchOptions
AnnotatedDecl
         ::= 'declare'  (CompatibilityAnnotation | Annotation)* ( VarDecl | FunctionDecl )
CompatibilityAnnotation 	   
         ::=  "updating"         
Annotation
         ::= '%' EQName ( '(' Literal ( ',' Literal )* ')' )?
VarDecl  ::= 'variable' '$' VarName TypeDeclaration? ( ':=' VarValue | 'external' ( ':=' VarDefaultValue )? )

VarValue ::= ExprSingle
VarDefaultValue
         ::= ExprSingle
ContextItemDecl
         ::= 'declare' 'context' 'item' ( 'as' ItemType )? ( ':=' VarValue | 'external' ( ':=' VarDefaultValue )? )
FunctionDecl
         ::= 'function' EQName '(' ParamList? ')' ( 'as' SequenceType )? ( FunctionBody | 'external' )
ParamList
         ::= Param ( ',' Param )*
Param    ::= '$' EQName TypeDeclaration?
FunctionBody
         ::= EnclosedExpr
EnclosedExpr
         ::= '{' Expr? '}'
OptionDecl
         ::= 'declare' 'option' EQName StringLiteral

FLWORExpr
         ::= InitialClause IntermediateClause* ReturnClause
InitialClause
         ::= ForClause
           | LetClause
           | WindowClause
IntermediateClause
         ::= InitialClause
           | WhereClause
           | GroupByClause
           | OrderByClause
           | CountClause
ForClause
         ::= 'for' ForBinding ( ',' ForBinding )*
ForBinding
         ::= '$' VarName TypeDeclaration? AllowingEmpty? PositionalVar? FTScoreVar? 'in' ExprSingle
AllowingEmpty
         ::= 'allowing' 'empty'
PositionalVar
         ::= 'at' '$' VarName
FTScoreVar
         ::= 'score' '$' VarName
LetClause
         ::= 'let' LetBinding ( ',' LetBinding )*
LetBinding
         ::= ( '$' VarName TypeDeclaration? | FTScoreVar ) ':=' ExprSingle
WindowClause
         ::= 'for' ( TumblingWindowClause | SlidingWindowClause )
TumblingWindowClause
         ::= 'tumbling' 'window' '$' VarName TypeDeclaration? 'in' ExprSingle WindowStartCondition WindowEndCondition?
SlidingWindowClause
         ::= 'sliding' 'window' '$' VarName TypeDeclaration? 'in' ExprSingle WindowStartCondition WindowEndCondition
WindowStartCondition
         ::= 'start' WindowVars 'when' ExprSingle
WindowEndCondition
         ::= 'only'? 'end' WindowVars 'when' ExprSingle
WindowVars
         ::= ( '$' CurrentItem )? PositionalVar? ( 'previous' '$' PreviousItem )? ( 'next' '$' NextItem )?
CurrentItem
         ::= EQName
PreviousItem
         ::= EQName
NextItem ::= EQName
CountClause
         ::= 'count' '$' VarName
WhereClause
         ::= 'where' ExprSingle
GroupByClause
         ::= 'group' 'by' GroupingSpecList
GroupingSpecList
         ::= GroupingSpec ( ',' GroupingSpec )*
GroupingSpec
         ::= GroupingVariable ( TypeDeclaration? ':=' ExprSingle )? ( 'collation' URILiteral )?
           | ExprSingle
GroupingVariable
         ::= '$' VarName
OrderByClause
         ::= ( 'order' 'by' | 'stable' 'order' 'by' ) OrderSpecList
OrderSpecList
         ::= OrderSpec ( ',' OrderSpec )*
OrderSpec
         ::= ExprSingle OrderModifier
OrderModifier
         ::= ( 'ascending' | 'descending' )? ( 'empty' ( 'greatest' | 'least' ) )? ( 'collation' URILiteral )?
ReturnClause
         ::= 'return' ExprSingle
QuantifiedExpr
         ::= ( 'some' | 'every' ) QuantifiedVarDecl ( ',' QuantifiedVarDecl )* 'satisfies' ExprSingle
QuantifiedVarDecl
         ::= '$' VarName TypeDeclaration? 'in' ExprSingle
SwitchExpr
         ::= 'switch' '(' Expr ')' SwitchCaseClause+ 'default' 'return' ExprSingle
SwitchCaseClause
         ::= ( 'case' SwitchCaseOperand )+ 'return' ExprSingle
SwitchCaseOperand
         ::= ExprSingle
TypeswitchExpr
         ::= 'typeswitch' '(' Expr ')' CaseClause+ 'default' ( '$' VarName )? 'return' ExprSingle
CaseClause
         ::= 'case' ( '$' VarName 'as' )? SequenceTypeUnion 'return' ExprSingle
SequenceTypeUnion
         ::= SequenceType ( '|' SequenceType )*

IfExpr   ::= 'if' '(' Expr ')' 'then' ExprSingle 'else' ExprSingle
BasexIfExpr   ::= 'if' '(' Expr ')' 'then' ExprSingle
/* BasexIfTenary   ::= ExprSingle '??' ExprSingle '!!' ExprSingle */
/* BasexElvis ::= ExprSingle '?:' ExprSingle  */

TryCatchExpr
         ::= TryClause CatchClause+
TryClause
         ::= 'try' EnclosedTryTargetExpr
EnclosedTryTargetExpr
         ::= EnclosedExpr
CatchClause
         ::= 'catch' CatchErrorList EnclosedExpr
CatchErrorList
         ::= NameTest ( '|' NameTest )*
OrExpr   ::= AndExpr ( 'or' AndExpr )*
AndExpr  ::= ComparisonExpr ( 'and' ComparisonExpr )*
ComparisonExpr
         ::= FTContainsExpr ( ( ValueComp | GeneralComp | NodeComp ) FTContainsExpr )?
FTContainsExpr
         ::= StringConcatExpr ( 'contains' 'text' FTSelection FTIgnoreOption? )?
StringConcatExpr
         ::= RangeExpr ( '||' RangeExpr )*
RangeExpr
         ::= AdditiveExpr ( 'to' AdditiveExpr )?
AdditiveExpr
         ::= MultiplicativeExpr ( ( '+' | '-' ) MultiplicativeExpr )*
MultiplicativeExpr
         ::= UnionExpr ( ( '*' | 'div' | 'idiv' | 'mod' ) UnionExpr )*
UnionExpr
         ::= IntersectExceptExpr ( ( 'union' | '|' ) IntersectExceptExpr )*
IntersectExceptExpr
         ::= InstanceofExpr ( ( 'intersect' | 'except' ) InstanceofExpr )*
InstanceofExpr
         ::= TreatExpr ( 'instance' 'of' SequenceType )?
TreatExpr
         ::= CastableExpr ( 'treat' 'as' SequenceType )?
CastableExpr
         ::= CastExpr ( 'castable' 'as' SingleType )?
CastExpr 
         ::=  TransformWithExpr ( 'cast' 'as' SingleType )?
TransformWithExpr
         ::= BasexUpdate1Expr ( 'transform' 'with' '{' Expr? '}' )?
 /* BaseX extension http://docs.basex.org/wiki/XQuery_Update#update */        
BasexUpdate1Expr
         ::= BasexUpdateExpr ( 'update' '{' Expr? '}')?
BasexUpdateExpr
         ::= ArrowExpr ( 'update' '{' Expr? '}' )*
/*  BaseX end */        
ArrowExpr
         ::= UnaryExpr ( '=>' ArrowFunctionSpecifier ArgumentList )*
UnaryExpr
         ::= ( '-' | '+' )* ValueExpr
ValueExpr
         ::= ValidateExpr
           | SimpleMapExpr
           | ExtensionExpr
SimpleMapExpr
         ::= PathExpr ('!' PathExpr)*
GeneralComp
         ::= '='
           | '!='
           | '<'
           | '<='
           | '>'
           | '>='
ValueComp
         ::= 'eq'
           | 'ne'
           | 'lt'
           | 'le'
           | 'gt'
           | 'ge'
NodeComp ::= 'is'
           | '<<'
           | '>>'
ValidateExpr
         ::= 'validate' ( ValidationMode | 'type' TypeName )? '{' Expr '}'
ValidationMode
         ::= 'lax'
           | 'strict'
ExtensionExpr
         ::= Pragma+ '{' Expr? '}'
Pragma   ::= '(#' S? EQName ( S PragmaContents )? '#)'
          /* ws: explicit */
PathExpr ::= '/' ( RelativePathExpr / )
           | '//' RelativePathExpr
           | RelativePathExpr
RelativePathExpr
         ::= StepExpr ( ( '/' | '//' | '!' ) StepExpr )*
StepExpr ::= PostfixExpr
           | AxisStep
AxisStep ::= ( ReverseStep | ForwardStep ) PredicateList
ForwardStep
         ::= ForwardAxis NodeTest
           | AbbrevForwardStep
ForwardAxis
         ::= 'child' '::'
           | 'descendant' '::'
           | 'attribute' '::'
           | 'self' '::'
           | 'descendant-or-self' '::'
           | 'following-sibling' '::'
           | 'following' '::'
AbbrevForwardStep
         ::= '@'? NodeTest
ReverseStep
         ::= ReverseAxis NodeTest
           | AbbrevReverseStep
ReverseAxis
         ::= 'parent' '::'
           | 'ancestor' '::'
           | 'preceding-sibling' '::'
           | 'preceding' '::'
           | 'ancestor-or-self' '::'
AbbrevReverseStep
         ::= '..'
NodeTest ::= KindTest
           | NameTest
NameTest ::= EQName
           | Wildcard
PostfixExpr
         ::= PrimaryExpr ( Predicate | ArgumentList | Lookup )*
ArgumentList
         ::= '(' ( Argument ( ',' Argument )* )? ')'
PredicateList
         ::= Predicate*
         
Predicate
         ::= '[' Expr ']'
Lookup  ::= '?' KeySpecifier
UnaryLookup ::= '?' KeySpecifier
KeySpecifier ::= NCName | IntegerLiteral | ParenthesizedExpr | '*'
ArrowFunctionSpecifier
         ::= EQName
           | VarRef
           | ParenthesizedExpr
Literal  ::= NumericLiteral
           | StringLiteral
NumericLiteral
         ::= IntegerLiteral
           | DecimalLiteral
           | DoubleLiteral
VarRef   ::= '$' VarName
VarName  ::= EQName
ParenthesizedExpr
         ::= '(' Expr? ')'
ContextItemExpr
         ::= '.'
OrderedExpr
         ::= 'ordered' '{' Expr '}'
UnorderedExpr
         ::= 'unordered' '{' Expr '}'
FunctionCall
         ::= FunctionEQName ArgumentList
Argument ::= ExprSingle
           | ArgumentPlaceholder
ArgumentPlaceholder
         ::= '?'
Constructor
         ::= DirectConstructor
           | ComputedConstructor
DirectConstructor
         ::= DirElemConstructor
           | DirCommentConstructor
           | DirPIConstructor
DirElemConstructor
         ::= '<' QName DirAttributeList ( '/>' | '>' DirElemContent* '</' QName S? '>' )
          /* ws: explicit */
DirAttributeList
         ::= ( S ( QName S? '=' S? DirAttributeValue )? )*
          /* ws: explicit */
DirAttributeValue
         ::= '"' ( EscapeQuot | QuotAttrValueContent )* '"'
           | "'" ( EscapeApos | AposAttrValueContent )* "'"
          /* ws: explicit */
QuotAttrValueContent
         ::= QuotAttrContentChar
           | CommonContent
AposAttrValueContent
         ::= AposAttrContentChar
           | CommonContent
DirElemContent
         ::= DirectConstructor
           | CDataSection
           | CommonContent
           | ElementContentChar
DirCommentConstructor
         ::= '<!--' DirCommentContents '-->'
          /* ws: explicit */
DirPIConstructor
         ::= '<?' PITarget ( S DirPIContents )? '?>'
          /* ws: explicit */
CDataSection
         ::= '<![CDATA[' CDataSectionContents ']]>'
          /* ws: explicit */          
ComputedConstructor
         ::= CompDocConstructor
           | CompElemConstructor
           | CompAttrConstructor
           | CompNamespaceConstructor
           | CompTextConstructor
           | CompCommentConstructor
           | CompPIConstructor
CompElemConstructor
         ::= 'element' ( EQName | '{' Expr '}' ) EnclosedContentExpr
EnclosedContentExpr
         ::= EnclosedExpr         
CompNamespaceConstructor
         ::= 'namespace' ( Prefix | '{' PrefixExpr '}' ) '{' URIExpr '}'
Prefix   ::= NCName
PrefixExpr
         ::= Expr
URIExpr  ::= Expr
FunctionItemExpr
         ::= NamedFunctionRef
           | InlineFunctionExpr
NamedFunctionRef
         ::= EQName '#' IntegerLiteral
InlineFunctionExpr
         ::= Annotation* 'function' '(' ParamList? ')' ( 'as' SequenceType )? FunctionBody
MapConstructor
         ::= 'map' '{' ( MapConstructorEntry ( ',' MapConstructorEntry )* )? '}'
MapConstructorEntry
         ::= MapKeyExpr ':' MapValueExpr
MapKeyExpr
         ::= ExprSingle
MapValueExpr
         ::= ExprSingle
ArrayConstructor
         ::= SquareArrayConstructor
           | CurlyArrayConstructor
SquareArrayConstructor
         ::= '[' ( ExprSingle ( ',' ExprSingle )* )? ']'
CurlyArrayConstructor
         ::= 'array' EnclosedExpr
StringConstructor
         ::= '``[' StringConstructorContent ']``'
          /* ws: explicit */
StringConstructorContent
         ::= StringConstructorChars ( StringConstructorInterpolation StringConstructorChars )*
          /* ws: explicit */
StringConstructorInterpolation
         ::= '`{' Expr? '}`'         
SingleType
         ::= SimpleTypeName '?'?
TypeDeclaration
         ::= 'as' SequenceType
SequenceType
         ::= 'empty-sequence' '(' ')'
           | ItemType ( OccurrenceIndicator / )

ItemType 	::=  	KindTest | ("item" "(" ")") | FunctionTest | MapTest | ArrayTest | AtomicOrUnionType | ParenthesizedItemType

OccurrenceIndicator
         ::= '?'
           | '*'
           | '+'

AtomicOrUnionType
         ::= EQName

KindTest ::= DocumentTest
           | ElementTest
           | AttributeTest
           | SchemaElementTest
           | SchemaAttributeTest
           | PITest
           | CommentTest
           | TextTest
           | NamespaceNodeTest
           | AnyKindTest
AnyKindTest 	   ::= "node" "(" ")" 	
DocumentTest 	   ::=    	"document-node" "(" (ElementTest | SchemaElementTest)? ")"
TextTest 	   ::= "text" "(" ")"
CommentTest 	   ::= "comment" "(" ")" 	
NamespaceNodeTest 	   ::= "namespace-node" "(" ")" 	
PITest 	   ::= "processing-instruction" "(" (NCName | StringLiteral)? ")" 	
AttributeTest 	   ::=    	"attribute" "(" (AttribNameOrWildcard ("," TypeName)?)? ")"
AttribNameOrWildcard 	   ::= AttributeName | "*" 	
SchemaAttributeTest 	   ::=    	"schema-attribute" "(" AttributeDeclaration ")" 
AttributeDeclaration 	   ::=    	AttributeName 	
ElementTest 	   ::=    	"element" "(" (ElementNameOrWildcard ("," TypeName "?"?)?)? ")"
ElementNameOrWildcard 	   ::=    	ElementName | "*" 
SchemaElementTest 	   ::=    	"schema-element" "(" ElementDeclaration ")"
ElementDeclaration 	   ::=    	ElementName
AttributeName 	   ::= EQName
ElementName 	   ::=    	EQName
SimpleTypeName::= TypeName
TypeName 	   ::= EQName
FunctionTest 	   ::= Annotation* (AnyFunctionTest
                   | TypedFunctionTest) 	
AnyFunctionTest 	   ::= "function" "(" "*" ")"
TypedFunctionTest 	   ::= "function" "(" (SequenceType ("," SequenceType)*)? ")" "as" SequenceType 	
MapTest 	   ::=    	AnyMapTest | TypedMapTest 	
AnyMapTest 	   ::=    	"map" "(" "*" ")" 	
TypedMapTest 	   ::=    	"map" "(" AtomicOrUnionType "," SequenceType ")" 	
ArrayTest 	   ::=    	AnyArrayTest | TypedArrayTest 	
AnyArrayTest 	   ::=    	"array" "(" "*" ")" 	
TypedArrayTest 	   ::=    	"array" "(" SequenceType ")"
ParenthesizedItemType 	   ::= "(" ItemType ")" 	
URILiteral ::= StringLiteral
EQName   ::= QName 
           | URIQualifiedName
FunctionEQName
         ::= FunctionName
           | URIQualifiedName
/* ======  */          
QName    ::= FunctionName
           | 'attribute'
           | 'comment'
           | 'document-node'
           | 'element'
           | 'empty-sequence'
           | 'function'
           | 'if'
           | 'item'
           | 'namespace-node'
           | 'node'
           | 'processing-instruction'
           | 'schema-attribute'
           | 'schema-element'
           | 'switch'
           | 'text'
           | 'typeswitch'
FunctionName
         ::= QName^Token
           | 'ancestor'
           | 'ancestor-or-self'
           | 'and'
           | 'as'
           | 'ascending'
           | 'at'
           | 'case'
           | 'cast'
           | 'castable'
           | 'child'
           | 'collation'
           | 'copy'
           | 'count'
           | 'declare'
           | 'default'
           | 'delete'
           | 'descendant'
           | 'descendant-or-self'
           | 'descending'
           | 'div'
           | 'document'
           | 'else'
           | 'empty'
           | 'end'
           | 'eq'
           | 'every'
           | 'except'
           | 'following'
           | 'following-sibling'
           | 'for'
           | 'ge'
           | 'group'
           | 'gt'
           | 'idiv'
           | 'import'
           | 'insert'
           | 'instance'
           | 'intersect'
           | 'invoke'
           | 'is'
           | 'le'
           | 'let'
           | 'lt'
           | 'mod'
           | 'module'
           | 'modify'
           | 'namespace'
           | 'ne'
           | 'only'
           | 'or'
           | 'order'
           | 'ordered'
           | 'parent'
           | 'preceding'
           | 'preceding-sibling'
           | 'rename'
           | 'replace'
           | 'return'
           | 'satisfies'
           | 'self'
           | 'some'
           | 'stable'
           | 'start'
           | 'to'
           | 'transform'
           | 'treat'
           | 'try'
           | 'union'
           | 'unordered'
           | 'update'
           | 'updating' 
           | 'validate'
           | 'where'
           | 'with'
           | 'xquery'                             
RevalidationDecl
         ::= 'declare' 'revalidation' ( 'strict' | 'lax' | 'skip' )
InsertExprTargetChoice
         ::= ( 'as' ( 'first' | 'last' ) )? 'into'
           | 'after'
           | 'before'
InsertExpr
         ::= 'insert' ( 'node' | 'nodes' ) SourceExpr InsertExprTargetChoice TargetExpr
DeleteExpr
         ::= 'delete' ( 'node' | 'nodes' ) TargetExpr
ReplaceExpr
         ::= 'replace' ( 'value' 'of' )? 'node' TargetExpr 'with' ExprSingle
RenameExpr
         ::= 'rename' 'node' TargetExpr 'as' NewNameExpr
SourceExpr
         ::= ExprSingle
TargetExpr
         ::= ExprSingle
NewNameExpr
         ::= ExprSingle
UpdatingFunctionCall
         ::= 'invoke'? 'updating' PrimaryExpr '(' ( ExprSingle ( ',' ExprSingle )* )? ')'
CopyModifyExpr
         ::= 'copy' CopyModifySpec ( ',' CopyModifySpec )* 'modify' ExprSingle 'return' ExprSingle
         
CopyModifySpec
         ::= '$' VarName ':=' ExprSingle

FTSelection
         ::= FTOr FTPosFilter*
FTWeight ::= 'weight' '{' Expr '}'
FTOr     ::= FTAnd ( 'ftor' FTAnd )*
FTAnd    ::= FTMildNot ( 'ftand' FTMildNot )*
FTMildNot
         ::= FTUnaryNot ( 'not' 'in' FTUnaryNot )*
FTUnaryNot
         ::= 'ftnot'? FTPrimaryWithOptions
FTPrimaryWithOptions
         ::= FTPrimary FTMatchOptions? FTWeight?
FTPrimary
         ::= FTWords FTTimes?
           | '(' FTSelection ')'
           | FTExtensionSelection
FTWords  ::= FTWordsValue FTAnyallOption?
FTWordsValue
         ::= StringLiteral
           | '{' Expr '}'
FTExtensionSelection
         ::= Pragma+ '{' FTSelection? '}'
FTAnyallOption
         ::= 'any' 'word'?
           | 'all' 'words'?
           | 'phrase'
FTTimes  ::= 'occurs' FTRange 'times'
FTRange  ::= 'exactly' AdditiveExpr
           | 'at' ( 'least' AdditiveExpr | 'most' AdditiveExpr )
           | 'from' AdditiveExpr 'to' AdditiveExpr
FTPosFilter
         ::= FTOrder
           | FTWindow
           | FTDistance
           | FTScope
           | FTContent
FTOrder  ::= 'ordered'
FTWindow ::= 'window' AdditiveExpr FTUnit
FTDistance
         ::= 'distance' FTRange FTUnit
FTUnit   ::= 'words'
           | 'sentences'
           | 'paragraphs'
FTScope  ::= ( 'same' | 'different' ) FTBigUnit
FTBigUnit
         ::= 'sentence'
           | 'paragraph'
FTContent
         ::= 'at' ( 'start' | 'end' )
           | 'entire' 'content'
FTMatchOptions
         ::= ( 'using' FTMatchOption )+
FTMatchOption
         ::= FTLanguageOption
           | FTWildCardOption
           | FTThesaurusOption
           | FTStemOption
           | FTCaseOption
           | FTDiacriticsOption
           | FTStopWordOption
           | FTExtensionOption
FTCaseOption
         ::= 'case' ( 'insensitive' | 'sensitive' )
           | 'lowercase'
           | 'uppercase'
FTDiacriticsOption
         ::= 'diacritics' ( 'insensitive' | 'sensitive' )
FTStemOption
         ::= 'stemming'
           | 'no' 'stemming'
FTThesaurusOption
         ::= 'thesaurus' ( FTThesaurusID | 'default' | '(' ( FTThesaurusID | 'default' ) ( ',' FTThesaurusID )* ')' )
           | 'no' 'thesaurus'
FTThesaurusID
         ::= 'at' URILiteral ( 'relationship' StringLiteral )? ( FTLiteralRange 'levels' )?
FTLiteralRange
         ::= 'exactly' IntegerLiteral
           | 'at' ( 'least' IntegerLiteral | 'most' IntegerLiteral )
           | 'from' IntegerLiteral 'to' IntegerLiteral
FTStopWordOption
         ::= 'stop' 'words' ( FTStopWords FTStopWordsInclExcl* | 'default' FTStopWordsInclExcl* )
           | 'no' 'stop' 'words'
FTStopWords
         ::= 'at' URILiteral
           | '(' StringLiteral ( ',' StringLiteral )* ')'
FTStopWordsInclExcl
         ::= ( 'union' | 'except' ) FTStopWords
FTLanguageOption
         ::= 'language' StringLiteral
FTWildCardOption
         ::= 'wildcards'
           | 'no' 'wildcards'
FTExtensionOption
         ::= 'option' EQName StringLiteral
FTIgnoreOption
         ::= 'without' 'content' UnionExpr

Comment  ::= '(:' ( CommentContents | Comment )* ':)'
          /* ws: explicit */
Whitespace
         ::= S^WS
           | Comment
          /* ws: definition */

NCName   ::= NCName^Token
           | 'and'
           | 'as'
           | 'ascending'
           | 'case'
           | 'cast'
           | 'castable'
           | 'collation'
           | 'count'
           | 'default'
           | 'descending'
           | 'div'
           | 'else'
           | 'empty'
           | 'end'
           | 'eq'
           | 'except'
           | 'for'
           | 'ge'
           | 'group'
           | 'gt'
           | 'idiv'
           | 'instance'
           | 'intersect'
           | 'is'
           | 'le'
           | 'let'
           | 'lt'
           | 'mod'
           | 'ne'
           | 'only'
           | 'or'
           | 'order'
           | 'return'
           | 'satisfies'
           | 'stable'
           | 'start'
           | 'to'
           | 'treat'
           | 'union'
           | 'updating'
           | 'where'

QueryBody
         ::= Expr
Expr     ::= ExprSingle ( ',' ExprSingle )*         
ExprSingle
        ::= FLWORExpr
           | QuantifiedExpr
           | SwitchExpr
           | TypeswitchExpr
           | IfExpr
           | BasexIfExpr
/*           | BasexIfTenary */
/*          | BasexElvis     */
           | TryCatchExpr
           | InsertExpr
           | DeleteExpr
           | RenameExpr
           | ReplaceExpr
           | UpdatingFunctionCall
           | CopyModifyExpr
           | OrExpr
      

CommonContent
         ::= PredefinedEntityRef
           | CharRef
           | '{{'
           | '}}'
           | EnclosedExpr

CompDocConstructor
         ::= 'document' EnclosedExpr
CompAttrConstructor
         ::= 'attribute' ( EQName | '{' Expr '}' ) EnclosedExpr
CompPIConstructor
         ::= 'processing-instruction' ( NCName | '{' Expr '}' ) EnclosedExpr
CompCommentConstructor
         ::= 'comment' EnclosedExpr
CompTextConstructor
         ::= 'text' EnclosedExpr
PrimaryExpr
         ::= Literal
           | VarRef
           | ParenthesizedExpr
           | ContextItemExpr
           | FunctionCall
           | OrderedExpr
           | UnorderedExpr
           | Constructor
           | FunctionItemExpr
           | MapConstructor
           | ArrayConstructor
           | StringConstructor
           | UnaryLookup


<?TOKENS?>


IntegerLiteral
         ::= Digits
DecimalLiteral
         ::= '.' Digits
           | Digits '.' [0-9]*
          /* ws: explicit */
DoubleLiteral
         ::= ( '.' Digits | Digits ( '.' [0-9]* )? ) [eE] [+#x002D]? Digits
          /* ws: explicit */
StringLiteral
         ::= '"' ( PredefinedEntityRef | CharRef | EscapeQuot | [^"&] )* '"'
           | "'" ( PredefinedEntityRef | CharRef | EscapeApos | [^'&] )* "'"
          /* ws: explicit */
URIQualifiedName
         ::= BracedURILiteral NCName
          /* ws: explicit */
BracedURILiteral
         ::= 'Q' '{' ( PredefinedEntityRef | CharRef | [^&{}] )* '}'
          /* ws: explicit */
PredefinedEntityRef
         ::= '&' ( 'lt' | 'gt' | 'amp' | 'quot' | 'apos' ) ';'
          /* ws: explicit */
EscapeQuot
         ::= '""'
EscapeApos
         ::= "''"
ElementContentChar
         ::= Char - [{}<&]
QuotAttrContentChar
         ::= Char - ["{}<&]
AposAttrContentChar
         ::= Char - ['{}<&]
PITarget ::= NCName - ( ( 'X' | 'x' ) ( 'M' | 'm' ) ( 'L' | 'l' ) )
NameStartChar
         ::= ':'
           | [A-Z]
           | '_'
           | [a-z]
           | [#x00C0-#x00D6]
           | [#x00D8-#x00F6]
           | [#x00F8-#x02FF]
           | [#x0370-#x037D]
           | [#x037F-#x1FFF]
           | [#x200C-#x200D]
           | [#x2070-#x218F]
           | [#x2C00-#x2FEF]
           | [#x3001-#xD7FF]
           | [#xF900-#xFDCF]
           | [#xFDF0-#xFFFD]
           | [#x10000-#xEFFFF]
NameChar ::= NameStartChar
           | '-'
           | '.'
           | [0-9]
           | #x00B7
           | [#x0300-#x036F]
           | [#x203F-#x2040]
Name     ::= NameStartChar NameChar*
CharRef  ::= '&#' [0-9]+ ';'
           | '&#x' [0-9a-fA-F]+ ';'
NCName   ::= Name - ( Char* ':' Char* )
QName    ::= PrefixedName
           | UnprefixedName
PrefixedName
         ::= Prefix ':' LocalPart
UnprefixedName
         ::= LocalPart
Prefix   ::= NCName
LocalPart
         ::= NCName
StringConstructorChars
         ::= ( Char* - ( Char* ( '`{' | ']``' ) Char* ) ) &( '`{' | ']`' )
          /* ws: explicit */
S        ::= ( #x0020 | #x0009 | #x000D | #x000A )+
Char     ::= #x0009
           | #x000A
           | #x000D
           | [#x0020-#xD7FF]
           | [#xE000-#xFFFD]
           | [#x10000-#x10FFFF]
Digits   ::= [0-9]+
CommentContents
         ::= ( ( Char+ - ( Char* ( '(:' | ':)' ) Char* ) ) - ( Char* '(' ) ) &':'
           | ( Char+ - ( Char* ( '(:' | ':)' ) Char* ) ) &'('
PragmaContents
         ::= ( Char* - ( Char* '#)' Char* ) ) &'#'
Wildcard ::= '*'
           | NCName ':*'
           | '*:' NCName
           | BracedURILiteral '*'
DirCommentContents
         ::= ( Char - '-' | '-' ( Char - '-' ) )*
DirPIContents
         ::= ( Char* - ( Char* '?>' Char* ) ) &'?'
CDataSectionContents
         ::= ( Char* - ( Char* ']]>' Char* ) ) &']]'
EOF      ::= $
NonNCNameChar
         ::= $
           | ':'
           | Char - NameChar
DelimitingChar
         ::= NonNCNameChar
           | '-'
           | '.'
DelimitingChar
          \\ DecimalLiteral DoubleLiteral IntegerLiteral
NonNCNameChar
          \\ URIQualifiedName NCName^Token QName^Token 'NaN' 'allowing' 'ancestor' 'ancestor-or-self' 'and' 'array' 'as' 'ascending' 'at' 'attribute' 'base-uri' 'boundary-space' 'by' 'case' 'cast' 'castable' 'catch' 'child' 'collation' 'comment' 'construction' 'context' 'copy' 'copy-namespaces' 'count' 'decimal-format' 'decimal-separator' 'declare' 'default' 'delete' 'descendant' 'descendant-or-self' 'descending' 'digit' 'div' 'document' 'document-node' 'element' 'else' 'empty' 'empty-sequence' 'encoding' 'end' 'eq' 'every' 'except' 'exponent-separator' 'external' 'first' 'following' 'following-sibling' 'for' 'function' 'ge' 'greatest' 'group' 'grouping-separator' 'gt' 'idiv' 'if' 'import' 'in' 'infinity' 'inherit' 'insert' 'instance' 'intersect' 'invoke' 'is' 'item' 'last' 'lax' 'le' 'least' 'let' 'lt' 'map' 'minus-sign' 'mod' 'modify' 'module' 'namespace' 'namespace-node' 'ne' 'next' 'no-inherit' 'no-preserve' 'node' 'of' 'only' 'option' 'or' 'order' 'ordered' 'ordering' 'parent' 'pattern-separator' 'per-mille' 'percent' 'preceding' 'preceding-sibling' 'preserve' 'previous' 'processing-instruction' 'rename' 'replace' 'return' 'satisfies' 'schema' 'schema-attribute' 'schema-element' 'self' 'sliding' 'some' 'stable' 'start' 'strict' 'strip' 'switch' 'text' 'then' 'to' 'treat' 'try' 'tumbling' 'type' 'typeswitch' 'union' 'unordered' 'updating' 'validate' 'value' 'variable' 'version' 'when' 'where' 'window' 'with' 'xquery' 'zero-digit'
'*'       << Wildcard
QName^Token
          << 'ancestor' 'ancestor-or-self' 'and' 'array' 'as' 'at' 'ascending' 'attribute' 'case' 'cast' 'castable' 'child' 'collation' 'comment' 'copy' 'count' 'declare' 'default' 'delete' 'descendant' 'descendant-or-self' 'descending' 'div' 'document' 'document-node' 'element' 'else' 'empty' 'empty-sequence' 'end' 'eq' 'every' 'except' 'following' 'following-sibling' 'for' 'function' 'ge' 'group' 'gt' 'idiv' 'if' 'import' 'insert' 'instance' 'intersect' 'invoke' 'is' 'item' 'le' 'let' 'lt' 'map' 'mod' 'modify' 'module' 'namespace' 'namespace-node' 'ne' 'node' 'only' 'or' 'order' 'ordered' 'parent' 'preceding' 'preceding-sibling' 'processing-instruction' 'rename' 'replace' 'return' 'satisfies' 'schema-attribute' 'schema-element' 'self' 'some' 'stable' 'start' 'switch' 'text' 'to' 'transform' 'treat' 'try' 'typeswitch' 'union' 'unordered' 'update' 'updating' 'validate' 'where' 'with' 'xquery'
NCName^Token
          << 'and' 'as' 'at' 'ascending'  'case' 'cast' 'castable' 'collation' 'count' 'default' 'delete' 'descending' 'div' 'else' 'empty' 'end' 'eq' 'except' 'first' 'for' 'ge' 'group' 'gt' 'idiv' 'insert' 'instance' 'intersect' 'is' 'last' 'le' 'let' 'lt' 'mod' 'modify' 'ne' 'only' 'or' 'order' 'rename' 'replace' 'return' 'satisfies' 'stable' 'start' 'to' 'transform' 'treat' 'union' 'update' 'updating' 'where' 'with'
          